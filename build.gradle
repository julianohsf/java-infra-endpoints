buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'com.bmuschko.nexus'
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'javax.servlet:javax.servlet-api:3.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    baseName = 'infra-endpoints'
    version = project.version
}

apply plugin: 'jacoco'

compileJava.dependsOn clean
build.dependsOn test
test.finalizedBy jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
        html.destination = new File("${buildDir}/reports/jacoco")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
                counter = 'INSTRUCTIONS'
            }
        }
    }
}

modifyPom {
    project {
        name 'java-infra-endpoints'
        description 'Infrastructure endpoints to increase observability in an ecossystem'
        url 'https://github.com/julianohsf/java-infra-endpoints'
        inceptionYear '2018'

        scm {
            url 'https://github.com/julianohsf/java-infra-endpoints'
            connection 'https://github.com/julianohsf/java-infra-endpoints.git'
            developerConnection 'git@github.com:julianohsf/java-infra-endpoints.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'julianohsf'
                name 'Juliano Fernandes'
                email 'me@julianohsf.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}